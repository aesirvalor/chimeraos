# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Ser <contact@emersion.fr>

# Enable checks in next release when edid-decode is not required for tests:
# https://gitlab.freedesktop.org/emersion/libdisplay-info/-/issues/35
BUILDENV+=(!check)

pkgname=libdisplay-info
pkgver=0.1.1
pkgrel=3.1
pkgdesc='EDID and DisplayID library'
url="https://gitlab.freedesktop.org/emersion/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(glibc)
makedepends=(hwdata
             meson
             ninja
             python)
provides=(libdisplay-info.so)
_archive="$pkgname-$pkgver"
source=("$url/-/releases/$pkgver/downloads/$_archive.tar.xz"
        cta-be-more-lenient.patch)
sha256sums=('0d8731588e9f82a9cac96324a3d7c82e2ba5b1b5e006143fefe692c74069fb60'
            '4407b2b5b9abe2c32a7bd982985d411f35b21b5a85438582bf161d91da3907b3')

prepare() {
  cd $pkgname-$pkgver
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
	arch-meson "$_archive" build
	ninja -C build
}

check() {
	meson test -C build --no-rebeuild --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$_archive/LICENSE"
}
